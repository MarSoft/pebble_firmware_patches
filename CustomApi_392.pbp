; This patch adds custom API support.
; Custom API reveals some otherwise not available methods
; to installed apps.
; NOTE: this may break some poorly written applications!
; In such case, app creator should be notified about a bug in the app:
; dict_write_end method should never be called with NULL parameter (usually).

; How is it done:
;
; - app calls `dict_write_end` method with NULL value.
; On stock firmwares, this method accepts only one parameter in R0
; and returns 0 if this parameter is zero, thus signalling error.
;
; But if this patch is installed, then that method call with `0` in R0
; will be handled specially.
; It will then accept at least 2 arguments,
; and second one denotes an API group.
;
; Group 0 is special and is used to check whether this patch is installed.
; A call `dict_write_end(0, 0)` will return 1 (true),
; meaning that custom APIs are supported.
;
; Other groups are documented below.

; dict_write_end method (partial signature)
48 b1 02 68 22 b1 83 68  23 b1 43 60 98 1a 70 47
{
	; was:
	; CBZ R0, ret
	; LDR R2, [R0]

	B.W my_dwe
	global dwe_cont

	; was:
	; CBZ R2, retR2
}

{
	proc my_dwe

	CBZ R0, our_case
	; not our case -> re-execute overwritten code
	LDR R2, [R0]
	B.W dwe_cont

	our_case:
	CBZ R1, group_0
	CMP R1, 1
	BEQ group_1
	CMP R1, 2
	BEQ group_2
	; ...

	; nothing matched -> group not supported, return 0 to signal failure
	MOV R0, 0
	BX LR

	group_0:
	; just return 1 (true) to signal that we are here
	MOV R0, 1
	BX LR

	group_1:
	B.W group1
	group_2:
	B.W group2
}

; Group 1 is a custom_call sub-api,
; it allows calling certain functions by its offset and signature.
; This approach is intended for debugging only,
; because it is bound to certain firmware&hardware version.
;
; R0, R1: (can be used internally)
; R2: offset
; R3: signature (first 4 bytes)
; [SP,0]: pointer to arguments list (actually will point to some stack pos)
; If signature didn't match, returns 0xBEBEBEBE.
; This can be used to check if this group is available in certain build:
; dict_write_end(0, 1, 0x8004000, 0) -> signature will not match.
{
	proc group1
	LDR R0, [R2]
	CMP R0, R3
	BEQ good

	; signature didn't match → return special value
	MOV R0, 0xBEBEBEBE
	BX LR

	good:
	; signature matched → load possible arguments and jump

	; first save func address to R4
	PUSH {R4,R5,LR}
	MOV R4, R2

	; We cannot know how many arguments we need,
	; so load all 4 of them to R0..R3

	; save current SP to R5
	MOV R5, SP
	; load arguments pos to R0
	LDR R0, [SP, 12] ; as [SP,{0,4,8}] are busy by push
	; update SP with this pos - make it point to our args
	MOV SP, R0
	; pop first 4 args (if any) to r0-r3
	; (it will just pop rubbish if none was in stack)
	POP {R0-R3}
	
	; now call!
	BLX R4

	; restore stack and return
	MOV SP, R5
	POP {R4,R5,PC}
}

{
	proc group2
	BX LR
}
