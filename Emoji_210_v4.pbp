; This patch makes it possible to support
; Emoji characters in custom fonts.
; It remaps Emoji range (0x1F6xx) to Private Use Area part (0xF6xx).
; It uses a mapping table for remap
; (by default table changes nothing, change it according to your fonts).
; There are two tables, main and simple.
; Simple table will be used if patcher is called with -D simple
; and is intended to have fewer mappings,
; for lightweight font.
#ver 210

; Parameters (-D):
; noremap — don't use remap table at all
; minimal — use minimal (first) remap table instead of maximal (second)

2d e9 f3 41 0f 0c 3f 04  04 46 0e 46 15 46 {
	proc text_resources_try_load_glyph_metadata 
	; was: PUSH.W R0,R1,R4-R8,LR
	; here R1 is codep, R3-R8 are unused
	B.W my_metadata
	global cont_metadata
}

2d e9 f7 43 05 46 0e 46  91 46 {
	proc text_resources_load_glyph_bitmap
	; was: PUSH.W R0-R2,R4-R9,LR
	; here R1 is codep, R3-R9 are unused
	B.W my_bitmap
	global cont_bitmap
}

; place our code
{
	proc my_metadata
	db 2d e9 f3 41 ; PUSH.W R0,R1,R4-R8,LR
	MOV R4, 1
	B.W begin

	global my_bitmap
	db 2d e9 f7 43 ; PUSH.W R0-R2,R4-R9,LR
	MOV R4, 0

	begin:
	; here R1 is codep,
	; R4 is tmp
	LDR R3, emoji_base
	CMP R1, R3
	BCC cont ; if codep < emoji_base then pass thru; BCC = BLO = unsigned lesser-than
	ADD R3, 0x4F ; emoji_count
	CMP R1, R3
	BHI cont ; if codep > emoji_max then pass thru (will terminate)
	LDR R3, emoji_base ; restore base in R3
	SUB R1, R3 ; now R1 is emoji-char number

#ifndef noremap
	; apply remap table...
	ADR R3, remap_table
	ADD R3, R1
	LDRB R1, [R3] ; now R1 is remapped
#endif

	LDR R3, new_base
	ADD R1, R3
	
	cont:
	CMP R4, 1
	BNE cont2
	B.W cont_metadata
	cont2:
	B.W cont_bitmap

	; data block:
	ALIGN 4
	emoji_base:	DCD 0x1F600
	new_base:	DCD 0xF600 ; part of "Private Use Area"
	; or for debug:
	;new_base:	DCD 0x28 ; "(" symbol

#ifndef noremap
	remap_table:
#ifdef minimal
	; simplified table, with lesser glyphs
	; to use it, launch patcher with "-D simple" arg
	db 00 01 02 03 04 05 06 07  08 09 0a 0b 0c 0d 0e 0f
	db 10 11 12 13 14 15 16 17  18 19 1a 1b 1c 1d 1e 1f
	db 20 21 22 23 24 25 26 27  28 29 2a 2b 2c 2d 2e 2f
	db 30 31 32 33 34 35 36 37  38 39 3a 3b 3c 3d 3e 3f
	db 40 41 42 43 44 45 46 47  48 49 4a 4b 4c 4d 4e 4f
#else
	; full table, with maximum glyphs
	db 00 01 02 03 04 05 06 07  08 09 0a 0b 0c 0d 0e 0f
	db 10 11 12 13 14 15 16 17  18 19 1a 1b 1c 1d 1e 1f
	db 20 21 22 23 24 25 26 27  28 29 2a 2b 2c 2d 2e 2f
	db 30 31 32 33 34 35 36 37  38 39 3a 3b 3c 3d 3e 3f
	db 40 41 42 43 44 45 46 47  48 49 4a 4b 4c 4d 4e 4f
#endif
#endif
}

